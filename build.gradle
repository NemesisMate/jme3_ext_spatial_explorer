plugins {
	id "java"
	id "maven-publish"
	id "eclipse"
	id "nebula.provided-base" version "2.2.2"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

version = "git describe --always --dirty".execute().text.trim()
println("version : '${version}'")

group = "com.github.davidb"
def description = "A debug tool for jmonkeyengine : display a window to browse the content of node (root of the scene by default)."

repositories {
	jcenter()
	maven { url "http://updates.jmonkeyengine.org/maven/"}
}

def v_jme3 = '3.0.10'

dependencies {
	compile 'org.controlsfx:controlsfx:8.40.9'
	provided "com.jme3:jme3-core:${v_jme3}"
	provided "com.jme3:jme3-desktop:${v_jme3}"
	provided "com.jme3:jme3-lwjgl:${v_jme3}"
	provided "com.jme3:jme3-bullet:${v_jme3}"
	testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

def pomConfig = {
	licenses {
		license {
			name "Public domain (CC0-1.0)"
			url "http://creativecommons.org/publicdomain/zero/1.0/"
			distribution "repo"
		}
	}
	developers {
		developer {
			id "davidB"
			name "David Bernard"
		}
	}
}

publishing {
	publications {
		mavenStuff(MavenPublication) {
			from components.java
			artifact sourcesJar
			pom.withXml {
				def root = asNode()
				root.appendNode('description', description)
				root.children().last() + pomConfig
			}
		}
	}
}

