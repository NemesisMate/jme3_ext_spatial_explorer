plugins {
	id "java"
	id "maven-publish"
	id "eclipse"
	id "nebula.provided-base" version "2.2.2"
}

group = "com.github.davidb"
def description = "A debug tool for jmonkeyengine : display a window to browse the content of node (root of the scene by default)."
version = "git describe --always --dirty".execute().text.trim()
println("version : '${version}'")

repositories {
	jcenter()
	maven { url "https://dl.bintray.com/jmonkeyengine/org.jmonkeyengine"}
}

//def jme3 = [v:'3.0.10', g:'com.jme3']
def jme3 = [v:'3.1.0-beta1', g:'org.jmonkeyengine']

dependencies {
	compile 'org.controlsfx:controlsfx:8.40.12'
	provided "${jme3.g}:jme3-core:${jme3.v}"
	provided "${jme3.g}:jme3-bullet:${jme3.v}"
	testCompile 'junit:junit:4.12'
	testRuntime "${jme3.g}:jme3-bullet-native:${jme3.v}"
	testRuntime "${jme3.g}:jme3-desktop:${jme3.v}"
	testRuntime "${jme3.g}:jme3-lwjgl:${jme3.v}"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

def pomConfig = {
	url "https://github.com/davidB/${project.name}"
	licenses {
		license {
			name "Public domain (CC0-1.0)"
			url "http://creativecommons.org/publicdomain/zero/1.0/"
			distribution "repo"
		}
	}
	developers {
		developer {
			id "davidB"
			name "David Bernard"
		}
	}
}

publishing {
	publications {
		mavenStuff(MavenPublication) {
			from components.java
			artifact sourcesJar
			pom.withXml {
				def root = asNode()
				root.appendNode('description', description)
				root.children().last() + pomConfig
			}
		}
	}
}

task run(type:JavaExec) {
	main = System.getProperty("exec.mainClass") ?: "samples.Demo"
	classpath = sourceSets.test.runtimeClasspath
}
